NEW
  AUTO 3,1
*--------------------------------------
* SYSTEM ERROR ROUTINES (VERSION = 1.1O )
* (DATE = 12/02/81)
*
* THIS MODULE CONTAINS THE SYSTEM ERROR AND SYSTEM FAILURE ROUTINES.
*
* DATA DECLARATIONS
*
SYSERR.S.SAVE	.EQ $09 				REGISTER SAVE AREA
SYSERR.PCH.SAVE	.EQ $08
SYSERR.PCL.SAVE	.EQ $07
SYSERR.P.SAVE	.EQ $06
SYSERR.A.SAVE	.EQ $05
SYSERR.X.SAVE	.EQ $04
SYSERR.Y.SAVE	.EQ $03
SYSERR.E.SAVE	.EQ $02
SYSERR.Z.SAVE	.EQ $01
SYSERR.B.SAVE	.EQ $00
*
NMI.VECTOR		.EQ $FFFA
*
TXT.CLR			.EQ $C050
MIX.CLR			.EQ $C052
HIRES.CLR		.EQ $C056
*
PG2.CLR			.EQ $C054
*
MSGBASE			.EQ $7E4
MSGBASE2		.EQ $BE4
MSG				.AS ' SYSTEM FAILURE = $'
MSGLEN			.EQ *-MSG
*
* SYSTEM ERROR ROUTINE
*
* THIS ROUTINE IS CALLED WHEN AN ERROR CONDITION HAS BEEN
* ENCOUNTERED. THE ERROR NUMBER IS PASSED IN THE A REG
* AND THE CALL TO THIS ROUTINE MUST ALWAYS BE A JSR.
*
SYSERR			sta	SERR
				pla
				sta SDEATH.REGS+SYSERR.PCL.SAVE
				pla
				sta SDEATH.REGS+SYSERR.PCH.SAVE
				sec
				lda SERR
				bne SERR.EXIT
				clc
SERR.EXIT		rts  					RETURNS ONE LEVEL BEYOND CALLER
*
* SYSTEM DEATH ROUTINE
*
* CALLED TO IMMEDIATELY TERMINATE EXECUTION OF THE MACHINE
* BECAUSE A FATAL ERROR HAS BEEN DETECTED BY THE OPERATING
* SYSTEM. THE ERROR CODE IS PASSED IN THE A REG. THE
* CALL TO THIS ROUTINE MUST ALWAYS BE A JSR.
*
SYSDEATH		sta	SDEATH.REGS+SYSERR.A.SAVE 	SAVE REGISTERS
				stx	SDEATH.REGS+SYSERR.X.SAVE
				sty	SDEATH.REGS+SYSERR.Y.SAVE
				php
				pla
				sta	SDEATH.REGS+SYSERR.P.SAVE
				tsx
				stx	SDEATH.REGS+SYSERR.S.SAVE
				lda	E.REG
				sta	SDEATH.REGS+SYSERR.E.SAVE
				lda	Z.REG
				sta	SDEATH.REGS+SYSERR.Z.SAVE
				lda	B.REG
				sta	SDEATH.REGS+SYSERR.B.SAVE
				pla
				sta	SDEATH.REGS+SYSERR.PCL.SAVE
				pla
				sta	SDEATH.REGS+SYSERR.PCH.SAVE

				sei				 		TURN OFF INTERRUPTS
				cld
*
				ldx	#0 					SAVE SYSTEM STACK PAGE IN PAGE $17
SD005			lda $100,X
				sta	$1700,X
				dex
				bne	SD005
*
				lda	$C059 				ENSURE SILENTYPE PORT SHUT DOWN
				lda	$C0DD
				lda	$C0DF
				lda	$C05F
				lda	$C05A
*
				lda	$C040 				SOUND BELL
*
				lda	#$74 				ENSURE RESET LOCK OFF & RAM SWITCHED IN.
				sta	E.REG
*
				lda	TXT.CLR 			SWITCH TO 40 COL B&W DISPLAY MODE
				lda	MIX.CLR
				lda	HIRES.CLR
				lda	PG2.CLR 			& SELECT PAGE 1
*
				lda	#$02
				bit	SCRNMODE
				bvs	SD015 				IF GRAPHICS MODE THEN KEEP 40 COL MODE
				beq	SD015 				IF 40 COL MODE THEN KEEP
				lda	MIX.CLR+1 			ELSE SWITCH TO 80 COL DISPLAY MODE
*
				ldx	#MSGLEN+1 			ENSURE BKGRND SET TO INVERSE SPACES
				lda	#$20 				SPACE CHAR W/INVERSE
SD010			sta MSGBASE2-1,X
				dex
				bpl	SD010
*
SD015			ldx #0 					MOVE MSG TO TEXT SCREEN
SD020			lda MSG,X
				sta	MSGBASE-1,X
				inx
				cpx #MSGLEN
				bne SD020
*
				lda SDEATH.REGS+SYSERR.A.SAVE 	DISPLAY ERROR CODE (2 HEX DIGITS)
				clc
				lsr
				lsr
				lsr
				lsr
				jsr PRINT 				FIRST DIGIT
				inx
				lda SDEATH.REGS+SYSERR.A.SAVE
				and #$0F
				jsr PRINT 				SECOND DIGIT
*
				lda #SD100
				sta NMI.VECTOR
				lda /SD100
				sta NMI.VECTOR+1
*
*
				jmp * 					HANG UNTIL REBOOT (CTRL/RESET)

SD100			rti  					NMI VECTOR POINT HERE TO MASK THEM OUT
*
*
* PRINT SUBROUTINE
*
PRINT			cmp #$A
				bcs PRNT100
				adc #$30 				"0"-"9"
				bcc PRNT110 			ALWAYS TAKEN
PRNT100			adc #$36 				"A"-"F"
PRNT110			sta MSGBASE-1,X
				rts
*--------------------------------------
MAN
SAVE /A3OSX.BUILD/SOS.13/sos.s.syserr
LOAD /A3OSX.BUILD/SOS.13/sos.s
ASM
