NEW
  AUTO 3,1
*--------------------------------------
*************************************************************************  
* APPLE /// SOS 1.3 SOURCE CODE FILE: DISK3.USEL.SRC  
*************************************************************************  
* ASSEMBLER: APPLE ][ 6502 ASSEMBLER from APPLE COMPUTER TOOLKIT  
				 
PAGE			  
REP				 40  
* NAME : UNITSEL  
* FUNCTION: SELECT & START A DRIVE,  
* SET UP MOTOR & SEEK DELAYS  
* INPUT : NONE  
* OUTPUT : MONTIME,SEEKTIME  
* DESTROYS: ALL REGISTERS  
REP				 40  
*  
UNITSEL			 EQU *  
LDY				 D.UNITNUM ;GET DRIVENUM  
LDA				 #0 ;ASSUME NO SEEKWAIT  
STA				 SEEKWAIT ; WILL BE NEEDED  
STA				 MONTIMEL ;CLEAR MONTIME  
STA				 MONTIMEH  
*  
* SEE IF MOTOR(S) STILL SPINNING:  
*  
JSR				 CHKDRV ;MOTOR(S) POWERED UP?  
BNE				 SPINNING ;=>YES. WHO IS IT?  
*  
* NO MOTOR(S) SPINNING. DESELECT  
* ALL MOTORS AND START AFRESH:  
*  
LDX				 MD.INT ;DESELECT ALL  
LDA				 #0 ;SHOW INTERNAL AS  
STA				 DRIVESEL+0 ; NOT SELECTED  
STA				 UPTIME+0 ;INDICATE DRIVE IS FULLY STOPPED  
JSR				 EXTDESEL ;DESELECT ALL EXTERNALS TOO  
JMP				 SETTIME ;GO SETUP MOTOR DELAY  
REP				 40  
* MOTOR(S) SPINNING: OURS?  
*  
SPINNING		 EQU *  
LDA				 DRIVESEL,Y ;HAD WE BEEN SELECTED?  
BNE				 GOFORIT ;=>YES, GO FOR IT RIGHT AWAY.  
*  
* WE AREN'T SPINNING. SHUTDOWN ANOTHER  
* DRIVE, IF NECESSARY, TO GET GOING:  
*  
CPY				 #0 ;ARE WE THE INTERNAL DRIVE?  
BEQ				 SETTIME ;=>YES, LEAVE EXT MOTOR ALONE  
*  
* WE'RE AN EXTERNAL DRIVE. STOP ALL EXTERNAL MOTORS  
* UNCONDITIONALLY, BUT LEAVE THE INTERNAL MOTOR ALONE.  
* IF WE *DID* HAVE TO STOP ANOTHER EXTERNAL, THEN  
* MAKE SURE WE SET THE CORRECT PRE-SEEK DELAY!  
*  
LDA				 #0 ;SEE IF ANOTHER EXTERNAL  
ORA				 DRIVESEL+3 ; HAD BEEN  
ORA				 DRIVESEL+2 ; SELECTED  
ORA				 DRIVESEL+1 ; BEFORE...  
BEQ				 SETTIME ;=>NO, SEEK DELAY IS UNNECESSARY  
INC				 SEEKWAIT ;YES, DELAY BEFORE STEPPING  
JSR				 EXTDESEL ;DESELECT ALL EXTERNALS  
JMP				 SETTIME ;=>GO SETUP MOTOR DELAY  
PAGE			  
REP				 40  
* OUR DRIVE IS SPINNING. GO FOR IT!  
* DEPENDING OF HOW LONG THE MOTOR'S BEEN ON,  
* THIS COMMAND MAY REQUIRE A MOTOR DELAY.  
*  
GOFORIT			 EQU *  
LDX				 D.COMMAND ;GET CURRENT COMMAND  
LDA				 MTIMES,X ;GET REQUIRED UPTIME FOR IT  
SEC				  
SBC				 UPTIME,Y ;DRIVE RUNNING LONG ENOUGH?  
BCS				 SELECT ;=>NO, AC NOW HAS DELTA-T  
LDA				 #0 ;OTHERWISE, WAIT=0  
JMP				 SELECT ;SET MONTIME & SELECT DRIVE  
REP				 40  
*   
* ALL MOTORS WERE OFF. CHOOSE THE   
* APPROPRIATE MOTOR-ON TIME:   
*   
SETTIME			 EQU *   
LDA				 #0 ;INDICATE THAT  
STA				 UPTIME,Y ; THE DRIVE WAS OFF  
LDX				 D.COMMAND ;GET CURRENT COMMAND  
LDA				 MTIMES,X ;GET CORRECT DELAY TIME  
REP				 40   
*   
* SELECT THE DRIVE & START IT:   
*   
SELECT			 EQU *   
STA				 MONTIMEH ;NEGATE IT BECAUSE  
LDA				 #0 ; IT GETS INCREMENTED  
SEC				 ; INSTEAD OF  
SBC				 MONTIMEH ; DECREMENTED  
STA				 MONTIMEH ;STUFF MOTOR DELAY  
CPY				 #1 ;ARE WE THE INTERNAL DRIVE?  
BCS				 SELEXT ;=>NO, AN EXTERNAL  
LDA				 IS.INT ;I/O SELECT INTERNAL  
LDA				 MS.INT ;MOTOR SELECT INTERNAL  
JMP				 UNITRET ;=>ALL DONE!  
*   
SELEXT			 EQU *   
LDA				 IS.EXT ;I/O SELECT EXTERNAL  
CPY				 #2 ;ARE WE 2, 3, OR 4 ?  
BCS				 NOTD2 ;=>DEFINITELY 3 OR 4  
LDA				 MD.EXT1 ;MOTOR SELECT  
LDA				 MS.EXT2 ; ONLY .D2  
JMP				 UNITRET ;=>ALL DONE!  
*   
NOTD2			 EQU *   
BNE				 ISD4 ;=>DEFINITELY NOT 3  
LDA				 MS.EXT1 ;MOTOR SELECT  
LDA				 MD.EXT2 ; ONLY .D3  
JMP				 UNITRET ;=>ALL DONE!  
*   
ISD4			 EQU *   
LDA				 MS.EXT1 ;MOTOR SELECT  
LDA				 MS.EXT2 ; ONLY .D4  
*   
*   
UNITRET			 EQU *   
LDA				 MOTORON ;PROVIDE MOTOR POWER  
LDA				 #1 ;SAY WE'VE SELECTED  
STA				 DRIVESEL,Y ; THIS DRIVE  
*   
* IF WE HAVE MOTORTIME TO BURN,   
* THEN DELAY 50 MS. THIS ENSURES   
* A GOOD SOLID CHKDRV AFTER   
* TURNING ON THE MOTOR.   
*   
LDA				 MONTIMEH ;ANY MOTORTIME?  
BPL				 UNITRTS ;=>NO, WE GO FOR IT.  
LDY				 #5 ;5*10 MS  
UNITDEL			 EQU *   
LDA				 #100 ;100*100US IS 10MS  
JSR				 MSWAIT   
DEY				   
BNE				 UNITDEL   
LDA				 #2 ;INCLUDE THE 50MS  
JSR				 ADDTIME ; IN MOTOR UPTIME(S)  
UNITRTS			 EQU *   
RTS				   
SKP				 5   
REP				 40   
* NAME : EXTDESEL  
* FUNCTION: DESELECT ALL EXTERNAL DRIVE MOTORS  
* INPUT : NONE  
* DESTROYS: AC,X  
				REP 40   
*     
EXTDESEL		 EQU *   
				LDA MD.EXT1 ;DESELECT ALL EXTERNAL  
				LDA MD.EXT2 ; DRIVE MOTORS  
				LDX #3 ;SHOW THAT THEY ARE  
				LDA #0 ; ARE ALL DEAD DUCKS  
EDS1			 STA DRIVESEL,X   
STA				 UPTIME,X ;DRIVE MOTORS ARE OFF  
DEX				    
BNE				 EDS1   
RTS				    
				   
CHN				 DISK3.SUBS.SRC   
				   
*************************************************************************  
* END OF APPLE /// SOS 1.3 SOURCE CODE FILE: DISK3.USEL.SRC  
*************************************************************************  
				 
*--------------------------------------
MAN
SAVE /A3OSX.BUILD/SOS.13\sos.s.disk3.usel.txt
LOAD /A3OSX.BUILD/SOS.13/sos.s
ASM
