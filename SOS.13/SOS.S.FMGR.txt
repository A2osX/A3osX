NEW
  AUTO 3,1
*--------------------------------------
*
* FILE MANAGER (VERSION = 1.1O )
* (DATE = 8/04/81)
*
* THIS MODULE IS ENTERED FROM THE SYSTEM CALL MANAGER, AND
* IS RESPONSIBLE FOR SWITCHING TO EITHER THE BLOCK FILE
* MANAGER, OR THE CHARACTER FILE MANAGER.
*
*F.TPARMX		.EQ $A0 ; LOC OF FILE SYSTEM CALL PARMS
*OPEN			.EQ $8
*CLOSE			.EQ $C
SETLEVEL		.EQ $12
GETLEVEL		.EQ $13
*F.REQCODE		.EQ F.TPARMX
F.LEVEL			.EQ F.TPARMX+$1
*PATHNAME		.EQ F.TPARMX+$1
*REFNUM			.EQ F.TPARMX+$1
PERIOD			.EQ $2E
LEVEL			.DA #1
*
* FILE MANAGER
*
FMGR			lda F.REQCODE
				cmp #OPEN
				bcc FMGR010
				beq FMGR020
				cmp #CLOSE
				bcc FMGR030
				beq FMGR040
				cmp #SETLEVEL
				beq SLEVEL
				cmp #GETLEVEL
				beq GLEVEL
*
FMGR010			jmp BFMGR ; EXIT
*
FMGR020			ldy #1
				lda (PATHNAME),Y
				cmp #PERIOD
				bne FMGR010
				jsr CFMGR
				bcc FMGR024
				lda SERR
				cmp #FNFERR
				beq FMGR026
FMGR024			rts  ; EXIT
*
FMGR026			lda #0
				sta SERR
				jmp BFMGR ; EXIT
*
FMGR030			lda REFNUM
FMGR031			bpl FMGR010
				jmp CFMGR ; EXIT
*
FMGR040			lda REFNUM
				bne FMGR031
				jsr BFMGR ; CLOSE (0)
				jmp CFMGR ; EXIT
*
SLEVEL			lda F.LEVEL
				beq LVL.ERR
				cmp #4
				bcs LVL.ERR
				sta LEVEL
				rts
LVL.ERR			lda #LVLERR
				jsr SYSERR
*
GLEVEL			ldy #0
				lda LEVEL
				sta (F.LEVEL),Y
				rts
*--------------------------------------
MAN
SAVE /A3OSX.BUILD/SOS.13/sos.s.fmgr
LOAD /A3OSX.BUILD/SOS.13/sos.s
ASM
